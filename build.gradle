buildscript {
    ext.kotlin_version = '1.2.21'
    ext.ktor_version = '0.9.1'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.0.0"
    }
}

plugins {
    id 'application'
    id 'java'
    id 'com.github.johnrengelman.shadow' version '2.0.2'
    id "com.palantir.docker" version "0.19.2"
}

apply plugin: 'kotlin'
apply plugin: 'org.junit.platform.gradle.plugin'

group 'com.jonnymatts'
version '0.1.0'

sourceCompatibility = 1.8

mainClassName = 'io.ktor.server.netty.DevelopmentEngine'

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/jetbrains/spek" }
    maven { url "http://dl.bintray.com/kotlin/ktor" }
    maven { url "https://dl.bintray.com/kotlin/kotlinx" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "io.ktor:ktor-jackson:$ktor_version"
    compile "ch.qos.logback:logback-classic:1.2.1"
    compile "io.github.microutils:kotlin-logging:1.4.9"
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompile ('org.jetbrains.spek:spek-api:1.1.5') {
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntime ('org.jetbrains.spek:spek-junit-platform-engine:1.1.5') {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
    testCompile "com.willowtreeapps.assertk:assertk:0.9"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'io.ktor.server.netty.DevelopmentEngine'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

shadowJar {
    baseName "mocktric"
}

docker {
    dependsOn shadowJar
    dockerfile file("Dockerfile")
    name "jonnymatts/mocktric:${version}"
    files tasks.shadowJar.outputs
}